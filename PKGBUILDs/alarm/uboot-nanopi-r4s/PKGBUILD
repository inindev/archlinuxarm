# U-Boot: NanoPi-R4s

buildarch=8

pkgname=uboot-nanopi-r4s
pkgver=2021.04
pkgrel=1
pkgdesc="U-Boot for NanoPi-R4s"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'git' 'python' 'swig' 'dtc' 'uboot-tools')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'https://github.com/atf-builds/atf/releases/download/v2.4/rk3399_bl31.elf'
        '0001-arm64-rk3399-Add-support-NanoPi-R4s.patch'
        'boot.txt'
        'mkscr')
md5sums=('1299c23ee4fde7e73a1b9f8f80fd28e0'
         '9c9026e8c1bff3924a0501edd0c553dd'
         '00cddc9f61661305b0cb3e514e078064'
         'a54bdadeafc54aad779b405b5d9adb2a'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd ${srcdir}/u-boot-${pkgver}

  rm -f arch/arm/dts/rk3399-nanopi-r4s-u-boot.dtsi
  rm -f arch/arm/dts/rk3399-nanopi-r4s.dts
  rm -f configs/nanopi-r4s-rk3399_defconfig
  patch -t -p1 < ../../0001-arm64-rk3399-Add-support-NanoPi-R4s.patch

  cp ${srcdir}/rk3399_bl31.elf ./bl31.elf

  cd ${srcdir}/u-boot-${pkgver}/configs
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> nanopi-r4s-rk3399_defconfig
}

build() {
  cd ${srcdir}/u-boot-${pkgver}

  unset CLFAGS CXXFLAGS CPPFLAGS LDFLAGS

  make nanopi-r4s-rk3399_defconfig
  make EXTRAVERSION=-${pkgrel}
  make EXTRAVERSION=-${pkgrel} u-boot.itb
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}/boot"

  tools/mkimage -n rk3399 -T rksd -d ./tpl/u-boot-tpl.bin "${pkgdir}/boot/rksd_loader.img"
  cat ./spl/u-boot-spl.bin >> "${pkgdir}/boot/rksd_loader.img"

  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}/boot/boot.scr"
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
